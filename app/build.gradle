apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.hyperether.pointsnaps"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 7
        versionName "1.0.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def Properties props = new Properties()
            def propFile = file('../local.properties')
            if (propFile.canRead()) {
                props.load(new FileInputStream(propFile))

                if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

                    android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
                    android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
                    android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
                    android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
                } else {
                    android.buildTypes.release.signingConfig = null
                }
            } else {
                android.buildTypes.release.signingConfig = null
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
    dataBinding {
        enabled = true
    }
    namespace 'com.hyperether.pointsnaps'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation files('libs/pointsnapssdk-release.aar')
    implementation('com.hyperether:android-toolbox:1.0.18@aar') {
        transitive = true
    }

    implementation(platform('org.jetbrains.kotlin:kotlin-bom:2.0.10'))
    implementation(platform('com.google.firebase:firebase-bom:33.1.2'))

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.gms:play-services-maps:19.0.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'

    implementation 'com.google.firebase:firebase-analytics:22.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:19.0.3'

    //Lifecycle components
    def lifecycle_version = '2.8.4'
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    //noinspection LifecycleAnnotationProcessorWithJava8
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.dhaval2404:imagepicker:2.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
